load("//tools:defaults.bzl", "http_server", "sass_binary")
load("//tools:defaults2.bzl", "ng_project")
load("@npm2//:defs.bzl", "npm_link_all_packages")
load("@aspect_rules_esbuild//esbuild:defs.bzl", "esbuild")

package(default_visibility = ["//visibility:public"])

npm_link_all_packages()

# List of dependencies that are referenced in the `index.html` file.
devserverIndexHtmlDependencies = [
    "@npm//zone.js",
    "@npm//kagekiri",
    "//src/material/prebuilt-themes:azure-blue",
    ":index.html",
    ":theme",
]

exports_files([
    "protractor.conf.js",
    "start-devserver.js",
    "devserver-configure.js",
])

ng_project(
    name = "e2e-app",
    testonly = True,
    srcs = glob(
        ["**/*.ts"],
    ),
    assets = glob(
        [
            "**/*.html",
            "**/*.css",
        ],
        exclude = ["index.html"],
    ),
    deps = [
        ":node_modules/@angular/cdk",
        ":node_modules/@angular/cdk-experimental",
        ":node_modules/@angular/components-examples",
        ":node_modules/@angular/material",
        "//:node_modules/@angular/animations",
        "//:node_modules/@angular/compiler",
        "//:node_modules/@angular/core",
        "//:node_modules/@angular/forms",
        "//:node_modules/@angular/platform-browser",
        "//:node_modules/@angular/router",
        "//src/cdk/testing/tests:test_components",
    ],
)

sass_binary(
    name = "theme",
    src = "theme.scss",
    deps = [
        "//src/material:sass_lib",
        "//src/material-experimental:sass_lib",
        "//src/material/core:theming_scss_lib",
    ],
)

esbuild(
    name = "bundles",
    testonly = True,
    srcs = [
        ":e2e-app",
        "//:node_modules/tslib",
    ],
    bundle = True,
    config = {
        # Note: `module` is needed for RxJS v6 dependency on tslib.
        "conditions": [
            "ng-linked",
            "module",
        ],
    },
    entry_point = ":main.js",
    format = "esm",
    metafile = True,
    output_dir = True,
    platform = "browser",
    sourcemap = "external",
    splitting = True,
    # We cannot use `ES2017` or high as that would result in `async/await` not being downleveled.
    # ZoneJS needs to be able to intercept these as otherwise change detection would not work properly.
    target = "es2016",
)

http_server(
    name = "server",
    testonly = True,
    srcs = devserverIndexHtmlDependencies,
    additional_root_paths = [
        "npm/node_modules",
    ],
    tags = ["manual"],
    deps = [
        ":bundles",
    ],
)
