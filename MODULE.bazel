"""Rules/toolchains for components with Bazel."""

module(
    name = "components",
)

bazel_dep(name = "yq.bzl", version = "0.3.4")
bazel_dep(name = "rules_nodejs", version = "6.7.3")
bazel_dep(name = "aspect_rules_js", version = "2.9.1")
bazel_dep(name = "rules_pkg", version = "1.2.0")
bazel_dep(name = "tar.bzl", version = "0.7.0")
bazel_dep(name = "aspect_bazel_lib", version = "2.22.5")
bazel_dep(name = "aspect_rules_esbuild", version = "0.25.0")
bazel_dep(name = "aspect_rules_jasmine", version = "2.0.2")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "aspect_rules_ts", version = "3.8.3")
bazel_dep(name = "bazel_skylib", version = "1.9.0")
bazel_dep(name = "rules_browsers")
git_override(
    module_name = "rules_browsers",
    commit = "5f0e50d17d15c70e6ab5546f1659338ae94c4072",
    remote = "https://github.com/devversion/rules_browsers.git",
)

bazel_dep(name = "rules_sass")
git_override(
    module_name = "rules_sass",
    commit = "1184a80751a21af8348f308abc5b38a41f26850e",
    remote = "https://github.com/devversion/rules_sass.git",
)

bazel_dep(name = "rules_angular")
git_override(
    module_name = "rules_angular",
    commit = "7133b97252508f8528e5c5818a9a73cacc2e2a0e",
    remote = "https://github.com/devversion/rules_angular.git",
)

bazel_dep(name = "devinfra")
git_override(
    module_name = "devinfra",
    commit = "a38ba21de078ce63bc878498a9354fe55a6eb3e3",
    remote = "https://github.com/angular/dev-infra.git",
)

yq = use_extension("@yq.bzl//yq:extensions.bzl", "yq")
use_repo(yq, "yq_toolchains")

rules_ts_ext = use_extension("@aspect_rules_ts//ts:extensions.bzl", "ext")
rules_ts_ext.deps(
    name = "components_npm_typescript",
    # Obtained by: curl --silent https://registry.npmjs.org/typescript/5.9.2 | jq -r '.dist.integrity'
    ts_integrity = "sha512-CWBzXQrc/qOkhidw1OzBTQuYRbfyxDXJMVJ1XNwUHGROVmuaeiEm3OslpZ1RV96d7SKKjZKrSJu3+t/xlw3R9A==",
    ts_version = "5.9.2",
)
use_repo(rules_ts_ext, **{"npm_typescript": "components_npm_typescript"})

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(
    node_repositories = {
        "22.22.0-darwin_arm64": ("node-v22.22.0-darwin-arm64.tar.gz", "node-v22.22.0-darwin-arm64", "5ed4db0fcf1eaf84d91ad12462631d73bf4576c1377e192d222e48026a902640"),
        "22.22.0-darwin_amd64": ("node-v22.22.0-darwin-x64.tar.gz", "node-v22.22.0-darwin-x64", "5ea50c9d6dea3dfa3abb66b2656f7a4e1c8cef23432b558d45fb538c7b5dedce"),
        "22.22.0-linux_arm64": ("node-v22.22.0-linux-arm64.tar.xz", "node-v22.22.0-linux-arm64", "1bf1eb9ee63ffc4e5d324c0b9b62cf4a289f44332dfef9607cea1a0d9596ba6f"),
        "22.22.0-linux_ppc64le": ("node-v22.22.0-linux-ppc64le.tar.xz", "node-v22.22.0-linux-ppc64le", "d83b9957431cc18e1fc143a4b99f89cde7b8a18f53ef392231b4336afd058865"),
        "22.22.0-linux_s390x": ("node-v22.22.0-linux-s390x.tar.xz", "node-v22.22.0-linux-s390x", "5aa0e520689448c4233e8d73f284e8e0634fdcd32b479735698494be5641f3e4"),
        "22.22.0-linux_amd64": ("node-v22.22.0-linux-x64.tar.xz", "node-v22.22.0-linux-x64", "9aa8e9d2298ab68c600bd6fb86a6c13bce11a4eca1ba9b39d79fa021755d7c37"),
        "22.22.0-windows_amd64": ("node-v22.22.0-win-x64.zip", "node-v22.22.0-win-x64", "c97fa376d2becdc8863fcd3ca2dd9a83a9f3468ee7ccf7a6d076ec66a645c77a"),
    },
    node_version = "22.22.0",
)
use_repo(node, "nodejs_toolchains")

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")
pnpm.pnpm(
    name = "pnpm",
    pnpm_version = "10.29.3",
    pnpm_version_integrity = "sha512-SY4ftMylqgbB3PJhHm+vxQly/+cYmZjECekN50VmREKY/+Q+bNKs3Hdboap8xeCSqLcFTIEbqMV3D4RpPTPS3A==",
)
use_repo(pnpm, "pnpm")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    custom_postinstalls = {
        "@angular/common": "node ../../@nginfra/angular-linking/index.mjs",
        "@angular/forms": "node ../../@nginfra/angular-linking/index.mjs",
        "@angular/localize": "node ../../@nginfra/angular-linking/index.mjs",
        "@angular/platform-browser": "node ../../@nginfra/angular-linking/index.mjs",
        "@angular/platform-server": "node ../../@nginfra/angular-linking/index.mjs",
        "@angular/router": "node ../../@nginfra/angular-linking/index.mjs",
    },
    data = [
        "//:package.json",
        "//:pnpm-workspace.yaml",
        "//integration:package.json",
        "//src/aria:package.json",
        "//src/cdk:package.json",
        "//src/cdk-experimental:package.json",
        "//src/components-examples:package.json",
        "//src/dev-app:package.json",
        "//src/e2e-app:package.json",
        "//src/google-maps:package.json",
        "//src/material:package.json",
        "//src/material-date-fns-adapter:package.json",
        "//src/material-experimental:package.json",
        "//src/material-luxon-adapter:package.json",
        "//src/material-moment-adapter:package.json",
        "//src/universal-app:package.json",
        "//src/youtube-player:package.json",
    ],
    npmrc = "//:.npmrc",
    package_visibility = {
        "@angular/aria": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
            "//src/components-examples:__subpackages__",
            "//src/dev-app:__subpackages__",
        ],
        "@angular/cdk": [
            "//docs:__subpackages__",
            "//integration:__subpackages__",
            "//src/aria:__subpackages__",
            "//src/cdk-experimental:__subpackages__",
            "//src/components-examples:__subpackages__",
            "//src/dev-app:__subpackages__",
            "//src/material-experimental:__subpackages__",
            "//src/material:__subpackages__",
            "//src/e2e-app:__subpackages__",
        ],
        "@angular/cdk-experimental": [
            "//docs:__subpackages__",
            "//integration:__subpackages__",
            "//src/components-examples:__subpackages__",
            "//src/dev-app:__subpackages__",
            "//src/material-experimental:__subpackages__",
            "//src/e2e-app:__subpackages__",
        ],
        "@angular/material": [
            "//docs:__subpackages__",
            "//integration:__subpackages__",
            "//src/components-examples:__subpackages__",
            "//src/dev-app:__subpackages__",
            "//src/material-experimental:__subpackages__",
            "//src/material-moment-adapter:__subpackages__",
            "//src/e2e-app:__subpackages__",
        ],
        "@angular/material-experimental": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
            "//src/components-examples:__subpackages__",
            "//src/dev-app:__subpackages__",
        ],
        "@angular/google-maps": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
            "//src/dev-app:__subpackages__",
        ],
        "@angular/youtube-player": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
            "//src/dev-app:__subpackages__",
        ],
        "@angular/material-moment-adapter": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
        ],
        "@angular/material-date-fns-adapter": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
        ],
        "@angular/material-luxon-adapter": [
            "//integration:__subpackages__",
            "//docs:__subpackages__",
            "//src/components-examples:__subpackages__",
        ],
    },
    pnpm_lock = "//:pnpm-lock.yaml",
)
use_repo(npm, "npm")

rules_angular = use_extension("@rules_angular//setup:extensions.bzl", "rules_angular")
rules_angular.setup(
    name = "components_rules_angular_configurable_deps",
    angular_compiler_cli = "//:node_modules/@angular/compiler-cli",
    typescript = "//:node_modules/typescript",
)
use_repo(rules_angular, **{"rules_angular_configurable_deps": "components_rules_angular_configurable_deps"})
